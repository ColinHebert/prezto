#
# A theme heavily using git_prompt_info.
#
# Authors:
#   Colin Hebert <hebert.colin@gmail.com>
#

function prompt_makkhdyn_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_makkhdyn_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  add-zsh-hook precmd prompt_makkhdyn_precmd

  prompt_makkhdyn_setup_git

  zstyle ':prezto:module:editor' completing '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:keymap:primary' overwrite ' %F{red}♺%f'
  zstyle ':prezto:module:editor:keymap' alternate ' %F{yellow}❮%f%B%F{red}❮%f%b%F{red}❮%f'

  PROMPT=''
  # The relative path '~/myFolder'
  PROMPT+='%F{green}%~'
  # The git prompt (see prompt_makkhdyn_setup_git)
  PROMPT+='${git_info:+${(e)git_info[prompt]}}'
  # '#' if the current user is root, '$' otherwise
  PROMPT+=' %B%F{red}%(!.#.$)'
  # Reset the colors and add a space
  PROMPT+='%b%f '

  RPROMPT=''
  # The number of jobs (if there is one job) '[Jobs: 42]'
  RPROMPT+='%1(j.%F{blue}[Jobs: %B%j%b%f%F{blue}]%f.)'
  # The editor keymap (when vim mode is enabled) '❮❮❮'
  RPROMPT+='${editor_info[keymap]}'
  # The editor override mode '♺'
  RPROMPT+='${editor_info[overwrite]}'
  # Last command exit code ('⏎' if fail)
  RPROMPT+='%(?:: %F{red}%B⏎%f)'

  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

# Set up the git_info configuration
# Only one prompt is created (git_info[prompt])
# Display the branch name if available, otherwise the position, otherwise the
#   current commit id
# Display the difference with remote ('<' and/or '>') or '=' if the branch is
#   up to date
function prompt_makkhdyn_setup_git {
  # %s - Special action name (am, merge, rebase).
  zstyle ':prezto:module:git' action '|%s'
  # %a - Indicator to notify of added files.
  zstyle ':prezto:module:git' added '+'
  # %A - Indicator to notify of ahead branch.
  zstyle ':prezto:module:git' ahead '>'
  # %B - Indicator to notify of behind branch.
  zstyle ':prezto:module:git' behind '<'
  # %b - Branch name.
  zstyle ':prezto:module:git' branch '%b'
  # %c - SHA-1 hash.
  zstyle ':prezto:module:git' commit '(%.7c...)'
  # %d - Indicator to notify of deleted files.
  zstyle ':prezto:module:git' deleted '-'
  # %m - Indicator to notify of modified files.
  zstyle ':prezto:module:git' modified '*'
  # %p - HEAD position in relation to the nearest branch, remote, tag.
  zstyle ':prezto:module:git' position '(%p)'
  # %R - Remote name.
  zstyle ':prezto:module:git' remote '='
  # %r - Indicator to notify of renamed files.
  zstyle ':prezto:module:git' renamed '^'
  # %S - Indicator to notify of stashed files.
  zstyle ':prezto:module:git' stashed '\$'
  # %U - Indicator to notify of unmerged files.
  zstyle ':prezto:module:git' unmerged '!'
  # %u - Indicator to notify of untracked files.
  zstyle ':prezto:module:git' untracked '%%%'

  # Prompts.
  zstyle ':prezto:module:git' info \
      prompt '%F{blue}($(coalesce "%b" "%p" "%c")%m%a%d%r%S%u%U$(coalesce "%B%A" "%R")%s)%f'
}

prompt_makkhdyn_setup "$@"
